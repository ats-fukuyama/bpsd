C
C     ****** TASK/W1 (SUBROUTINE LIBRARY 1) ******
C
C     ******* OUTPUT TO LINE PRINTER *******
C
      SUBROUTINE W1PRNT(NPRINTL)
C
      USE w1comm
      IMPLICIT NONE
      INTEGER,INTENT(IN):: NPRINTL
      INTEGER:: NA,NX,NXANT1,NXANT2,NS,NZ,IC,IFFT,LP,I
      REAL(rkind):: RNORM,PJK
      CHARACTER(LEN=8):: INDEX
C
      RNORM=1.D0/PANT
      IF(NPRINTL.LE.-1) THEN
         WRITE(6,611) (PROFPN(NX,4),NX=1,NXP)
  611    FORMAT(1H ,'*** ALPHA PARTICLE DENSITY PROFILE ***'/
     &         (1H ,1P10D12.4))
      ENDIF
C
      WRITE(6,620) PANT*RNORM,PANT1*RNORM,PANT2*RNORM
      WRITE(6,661) PWALL*RNORM,PWALL1*RNORM,PWALL2*RNORM,
     &             PABSTT*RNORM,AJCDT*RNORM,PERR*RNORM
      WRITE(6,664)
      WRITE(6,663) (NS,PABSXZ(NS)*RNORM,
     &              AHLT(NS,1)*RNORM,AHLT(NS,2)*RNORM,
     &             (AHLT(NS,1)+AHLT(NS,2))*RNORM,
     &              AHLT(NS,3)*RNORM,AHLT(NS,4)*RNORM,
     &             (AHLT(NS,3)+AHLT(NS,4))*RNORM,NS=1,NSMAX)
      WRITE(6,662)
      DO 200 NA = 1 , NAMAX
  200 WRITE(6,621) NA,RANT1(NA),XANT1(NA),RANT2(NA),XANT2(NA)
C
      DO 2000 NX=1,NXT
         IF(NX.LE.NXV) THEN
            NXPRNT(NX)=NXP+NXV+NX
         ELSE
            NXPRNT(NX)=NX-NXV
         ENDIF
 2000 CONTINUE
C
      IF(NPRINTL.LE.0) RETURN
C
      NXANT1=NXP+NXV+NXV/2+1
      NXANT2=NXP    +NXV/2
      WRITE(6,633)
      WRITE(6,601)
      DO 3100 NZ=1,NZP
         WRITE(6,634) ZA(NZ),CJ1(NZ),CE2DA(NZ,NXANT1,2),
     &                       CJ2(NZ),CE2DA(NZ,NXANT2,2)
 3100 CONTINUE
C
      LP=LOG(DBLE(NZP))/LOG(2.D0)+0.5D0
      IFFT=1
      CALL W1FFTL(CJ1,NZP,0)
      CALL FFT2L(CJ2,CFJ2,CT,LNST,NZP/2,IFFT,1,LP)
      WRITE(6,635) (NS,NS=1,NSMAX)
      WRITE(6,601)
      DO 3200 NZ=1,NZP
         PJK=ABS(CJ1(NZ))**2+ABS(CJ2(NZ))**2
         WRITE(6,636) AKZ(NZ),PJK*RNORM,PANTK(NZ)*RNORM,
     &                (PAKT(NZ,I)*RNORM,I=1,3),
     &                (PAK(NZ,NS)*RNORM,NS=1,NSMAX)
 3200 CONTINUE
      CALL W1FFTL(CJ1,NZP,1)
      CALL FFT2L(CFJ2,CJ2,CT,LNST,NZP/2,IFFT,2,LP)
C
      IF(NPRINTL.LE.1) RETURN
C
      WRITE(6,630) (NS,NS=1,NSMAX)
      WRITE(6,601)
      DO 3000 NX=1,NXP
         WRITE(6,631) XAM(NX),FLUXX(NX),
     &                (PABSX(NX,NS),NS=1,NSMAX)
 3000 CONTINUE
      WRITE(6,632) PABSTT,(PABSXZ(NS),NS=1,NSMAX)
      WRITE(6,6321)       (PABSXZ(NS)/PABSTT,NS=1,NSMAX)
C
      IF(NPRINTL.LE.2) RETURN
C
      DO 4200 IC=1,3
C
         DO 4110 NX=1,NXT
         DO 4110 NZ=1,NZP
            TEMP(NX,NZ)=ABS(CE2DA(NZ,NX,IC))
 4110    CONTINUE
         IF(IC.EQ.1) INDEX='ABS(EX) '
         IF(IC.EQ.2) INDEX='ABS(EY) '
         IF(IC.EQ.3) INDEX='ABS(EZ) '
         CALL W1PRTS(NXT,NZP,1,INDEX)
C
         IF(NPRINTL.LE.3) RETURN
C
         DO 4120 NX=1,NXT
         DO 4120 NZ=1,NZP
            TEMP(NX,NZ)=REAL(CE2DA(NZ,NX,IC))
 4120    CONTINUE
         IF(IC.EQ.1) INDEX='REAL(EX)'
         IF(IC.EQ.2) INDEX='REAL(EY)'
         IF(IC.EQ.3) INDEX='REAL(EZ)'
         CALL W1PRTS(NXT,NZP,1,INDEX)
C
         DO 4130 NX=1,NXT
         DO 4130 NZ=1,NZP
            TEMP(NX,NZ)=IMAG(CE2DA(NZ,NX,IC))
 4130    CONTINUE
         IF(IC.EQ.1) INDEX='IMAG(EX)'
         IF(IC.EQ.2) INDEX='IMAG(EY)'
         IF(IC.EQ.3) INDEX='IMAG(EZ)'
         CALL W1PRTS(NXT,NZP,1,INDEX)
C
 4200 CONTINUE
      RETURN
C
  601 FORMAT(1H )
  620 FORMAT(1H0,'*** POWER PARTITION *** (UNIT = MW/M)'/
     &       1H ,4X,'PANT   =',1PE12.4,8X,
     &              'PANT1  =',  E12.4,4X,'PANT2  =',E12.4)
  661 FORMAT(1H ,4X,'PWALL  =',1PE12.4,8X,
     &              'PWALL1 =',  E12.4,4X,'PWALL2 =',E12.4/
     &       1H ,4X,'PABST  =',1PE12.4,8X,'AJCDT  =',E12.4,
     &           4X,'PERR   =',1PE12.4)
  663 FORMAT(1H ,I5,1P7E10.2)
  662 FORMAT(1H ,'*** ANTENNA LOADING *** (UNIT = OHM/M)'/
     &       1H ,'NA  ',7X,'RANTH',10X,'XANTH',15X,'RANTL',10X,'XANTL')
  664 FORMAT(1H ,'   NS   PABS       I R      I NR      I TOT',
     &                        '      I RT     I NRT     I TOTT')
  621 FORMAT(1H ,1X,I3,2X,2(3X,1PE12.4),5X,2(3X,E12.4))
  630 FORMAT(1H0,5X,'X',10X,'FLUXX(X)',1X,
     &           2X,5(6X,'P-NS',I1,'(X)':))
  631 FORMAT(1H ,F10.5,1PE14.4,2X,5(E14.4))
  632 FORMAT(1H0,3X,'TOTAL',18X,1PE14.4,2X,5(E14.4))
 6321 FORMAT(1H ,42X,5(F14.5))
  633 FORMAT(1H0,5X,'Z',20X,'CJ1',23X,'EY1',25X,'CJ2',23X,'EY2')
  634 FORMAT(1H ,F10.5,2(4X,1P2E12.4,2X,2E12.4))
  635 FORMAT(1H0,3X,'KZ',6X,'J**2(KZ)',4X,'P-ANT(KZ)',3X,'P-TOT(KZ)',
     &           3X,'P-IBW(KZ)',3X,'P-ABS(KZ)',
     &           2X,5(3X,'P-NS',I1,'(KZ)':))
  636 FORMAT(1H ,F8.4,1PE12.4,E12.4,E12.4,E12.4,E12.4,2X,5(E12.4))
      END
C
C     ====== OUTPUT OF 2-D DATA TO LINE PRINTER ======
C
      SUBROUTINE W1PRTS(NX,NZ,IND,INDEX)
C
      USE w1comm
      IMPLICIT NONE
      INTEGER,INTENT(IN):: NX,NZ,IND
      CHARACTER(LEN=8),INTENT(IN):: INDEX
      INTEGER:: IZ,IEND,KX,K,I
C
      DO 10 IZ=1,NZ,9
         IEND=IZ+8
         IF(IEND.GT.NZ) IEND=NZ
         WRITE(6,601) INDEX
         WRITE(6,602) (ZA(I),I=IZ,IEND)
         WRITE(6,603)
      DO 10 KX=1,NX
         K=KX
         IF(IND.EQ.1) K=NXPRNT(KX)
         WRITE(6,604) XAM(K),(TEMP(K,I),I=IZ,IEND)
   10 CONTINUE
      RETURN
  601 FORMAT(1H0,A8)
  602 FORMAT(1H ,'        Z=',9(F8.4,4X))
  603 FORMAT(1H ,'   X')
  604 FORMAT(1H ,F8.4,2X,9(1PE12.4))
      END
C
C     ******* OUTPUT TO FILE 21 *******
C
      SUBROUTINE W1FILE(NFILEL)
C
      USE w1comm
      IMPLICIT NONE
      INTEGER,INTENT(IN):: NFILEL
      CHARACTER(LEN=8):: INDEX
      INTEGER:: NS,NX,I,NZ,IC
      REAL(rkind):: AZ
C
      IF(NFILEL.LE.0) RETURN
C
      AZ=0.D0
      WRITE(21) REAL(RF),REAL(BB),REAL(AJYH(1)),REAL(AJYL(1)),
     &          REAL(RR),REAL(RA),REAL(RD),REAL(RB),REAL(RZ),
     &          REAL(AZ),REAL(WALLR),REAL(RKZ)
      WRITE(21) NXP,NXV,NZP,NSMAX
      WRITE(21) (REAL(PA(NS)),REAL(PZ(NS)),REAL(PN(NS)),
     &           REAL(PTPP(NS)),REAL(PTPR(NS)),REAL(PU(NS)),
     &           REAL(PNS(NS)),REAL(PTS(NS)),REAL(PZCL(NS)),
     &           NS=1,NSMAX)
      WRITE(21) (REAL(XAM(NX)),NXPRNT(NX),NX=1,NXT)
C
      WRITE(21) PANT,PANT1,PANT2,PWALL,PWALL1,PWALL2,PIBW,PIBW1,PIBW2,
     &          PABSTT,(PABSXZ(NS),NS=1,NSMAX),PERR,
     &          RANT1,RANT2,XANT1,XANT2
      WRITE(21) (FLUXX(NX),NX=1,NXT)
      WRITE(21) ((PABSX(NX,NS),NX=1,NXP),NS=1,NSMAX)
C
      WRITE(21) (REAL(ZA(NZ)),NZ=1,NZP)
      WRITE(21) (REAL(AKZ(NZ)),NZ=1,NZP)
      WRITE(21) (PANTK(NZ),(PAKT(NZ,I),I=1,3),
     &          (PAK(NZ,NS),NS=1,NSMAX),NZ=1,NZP)
C
      IF(NFILEL.LE.1) GOTO 9000
C
      DO 8100 IC=1,3
         IF(IC.EQ.1) INDEX='EFIELD-X'
         IF(IC.EQ.2) INDEX='EFIELD-Y'
         IF(IC.EQ.3) INDEX='EFIELD-Z'
         WRITE(21) INDEX
C        WRITE(21) ((CE2DA(NZ,NX,IC),NX=1,NXT),NZ=1,NZP)
         WRITE(21) ((REAL(CE2DA(NZ,NX,IC)),
     &               IMAG(CE2DA(NZ,NX,IC)),NX=1,NXT),NZ=1,NZP)
 8100 CONTINUE
C
C
 9000 INDEX='END-DATA'
      WRITE(21) INDEX
      WRITE(6,680)
      RETURN
  680 FORMAT(1H0,'***** DATA SAVED IN FILE 21 *****'/)
      END
