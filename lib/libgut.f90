!     $Id$
      MODULE GSCTR5
        IMPLICIT NONE
        INTEGER(4) :: NXAC, NXMAXC, NYMAXC
      END MODULE

!     ***********************************************************

!           CEILING FUNCTION FOR LOG10 PLOT

!     ***********************************************************

      REAL(8) FUNCTION PLOG(X,XMIN,XMAX)

      IMPLICIT NONE
      REAL(8), INTENT(IN) ::  X, XMIN, XMAX

      IF(X.LT.XMIN) THEN
         PLOG=LOG10(XMIN)
      ELSEIF(X.GT.XMAX) THEN
         PLOG=LOG10(XMAX)
      ELSE
         PLOG=LOG10(X)
      ENDIF

      RETURN
      END FUNCTION PLOG

!     *****************************

!     INTERPORATE RGB

!     *****************************

      SUBROUTINE GUSRGB(GL,GRGBL,NRGB,GLA,GRGBLA)

      IMPLICIT NONE
      REAL(4),                  INTENT(IN) :: GL
      REAL(4), DIMENSION(3),    INTENT(OUT):: GRGBL
      INTEGER(4)              , INTENT(IN) :: NRGB
      REAL(4), DIMENSION(NRGB), INTENT(IN) :: GLA
      REAL(4), DIMENSION(3,NRGB),INTENT(IN):: GRGBLA
      REAL(4)    :: GFACT
      INTEGER(4) :: N, NDO

      DO NDO=2,NRGB
         N=NDO
         IF(GLA(N).GT.GL) GOTO 9
      ENDDO
    9 CONTINUE

      GFACT=(GL-GLA(N-1))/(GLA(N)-GLA(N-1))
      GRGBL(1)=GRGBLA(1,N-1)*(1.0-GFACT)+GRGBLA(1,N)*GFACT
      GRGBL(2)=GRGBLA(2,N-1)*(1.0-GFACT)+GRGBLA(2,N)*GFACT
      GRGBL(3)=GRGBLA(3,N-1)*(1.0-GFACT)+GRGBLA(3,N)*GFACT

      RETURN
      END SUBROUTINE GUSRGB

!     ****** CONTOUR PLOT : XY, VARIABLE POSITION, PATTERN ******

      SUBROUTINE CONTR5(Z,X,Y,NXA,NXMAX,NYMAX, &
     &                  ZORG,ZSTEP,NSTEP)

      USE GSCTR5
      IMPLICIT NONE
      INTEGER(4), INTENT(INOUT) :: NXA, NXMAX, NYMAX, NSTEP
      REAL(4),    INTENT(INOUT) :: ZORG, ZSTEP
      REAL(4), DIMENSION(NXA,*), INTENT(INOUT) :: Z, X, Y
      REAL(4)  :: EPS = 1.E-32
      EXTERNAL CONTS5

      IF(ABS(ZSTEP).LT.EPS) RETURN

      NXAC=NXA
      NXMAXC=NXMAX
      NYMAXC=NYMAX
      CALL CONTR0(Z,X,Y,NXA,NXMAX,NYMAX, &
     &            ZORG,ZSTEP,NSTEP,2,CONTS5)
      RETURN
      END SUBROUTINE CONTR5

!     ****** CONTOUR PLOT : XY, VARIABLE POSITION, PATTERN ******

      SUBROUTINE CONTP5(Z,X,Y,NXA,NXMAX,NYMAX, &
     &                  ZORG,ZSTEP,NSTEP,IPRD,IPAT,KA)

      USE GSCTR5
      IMPLICIT NONE
      INTEGER(4), INTENT(INOUT) :: NXA, NXMAX, NYMAX, NSTEP, IPRD, IPAT
      INTEGER(4), DIMENSION(2,*), INTENT(INOUT) :: KA
      REAL(4),    INTENT(INOUT) :: ZORG, ZSTEP
      REAL(4),  DIMENSION(NXA,*), INTENT(INOUT) :: Z, X, Y
      REAL(4)  :: EPS = 1.E-32
      EXTERNAL CONTS5


      IF(ABS(ZSTEP).LT.EPS) RETURN

      NXAC=NXA
      NXMAXC=NXMAX
      NYMAXC=NYMAX
      CALL CONTP0(Z,X,Y,NXA,NXMAX,NYMAX, &
     &            ZORG,ZSTEP,NSTEP,IPRD,IPAT,KA,CONTS5)
      RETURN
      END SUBROUTINE CONTP5

!     ****** CONTOUR PLOT : XY, VARIABLE POSITION, PATTERN ******

      SUBROUTINE CONTF5(Z,X,Y,NXA,NXMAX,NYMAX, &
     &                  ZORG,ZSTEP,NSTEP,IPRD,IPAT,KA)

      USE GSCTR5
      IMPLICIT NONE
      INTEGER(4), INTENT(INOUT) :: NXA, NXMAX, NYMAX, NSTEP, IPRD, IPAT
      INTEGER(4), DIMENSION(4,*), INTENT(INOUT) :: KA
      REAL(4),    INTENT(INOUT) :: ZORG, ZSTEP
      REAL(4),  DIMENSION(NXA,*), INTENT(INOUT) :: Z, X, Y
      REAL(4)  :: EPS = 1.E-32
      EXTERNAL CONTS5

      IF(ABS(ZSTEP).LT.EPS) RETURN

      NXAC=NXA
      NXMAXC=NXMAX
      NYMAXC=NYMAX
      CALL CONTQ0(Z,X,Y,NXA,NXMAX,NYMAX, &
     &            ZORG,ZSTEP,NSTEP,IPRD,IPAT,KA,CONTS5)
      RETURN
      END SUBROUTINE CONTF5

!     ****** CONTOUR PLOT : XY, VARIABLE POSITION, PATTERN ******

      SUBROUTINE CONTQ5(Z,X,Y,NXA,NXMAX,NYMAX, &
     &                  ZORG,ZSTEP,NSTEP,IPRD,IPAT,KA)

      USE GSCTR5
      IMPLICIT NONE
      INTEGER(4), INTENT(INOUT) :: NXA, NXMAX, NYMAX, NSTEP, IPRD, IPAT
      INTEGER(4), DIMENSION(4,*), INTENT(INOUT) :: KA
      REAL(4),    INTENT(INOUT) :: ZORG, ZSTEP
      REAL(4),  DIMENSION(NXA,*), INTENT(INOUT) :: Z, X, Y
      REAL(4)  :: EPS = 1.E-32
      EXTERNAL CONTS5


      IF(ABS(ZSTEP).LT.EPS) RETURN

      NXAC=NXA
      NXMAXC=NXMAX
      NYMAXC=NYMAX
      CALL CONTQ0(Z,X,Y,NXA,NXMAX,NYMAX, &
     &            ZORG,ZSTEP,NSTEP,IPRD,IPAT,KA,CONTS5)
      RETURN
      END SUBROUTINE CONTQ5

!     ****** CONTOUR PLOT SLAVE ROUTINE : XY VARIABLE ******

      SUBROUTINE CONTS5(NAX,NAY,NBX,NBY,U0,UA,UB,X,Y,IPAT,IND)

      USE GSCTR5
      IMPLICIT NONE
      INTEGER(4), INTENT(INOUT) :: NAX, NAY, NBX, NBY, IPAT, IND
      REAL(4),    INTENT(INOUT) :: U0, UA, UB
      REAL(4), DIMENSION(*), INTENT(INOUT) ::  X, Y
      INTEGER(4) :: NA, NA1, NA2, NAXL, NAYL, NAX1, NAY1, NB, NB1, NB2, NBXL, NBYL, NBX1, NBY1
      REAL(4)    :: DX, DY, PXMAX, PXMIN, PYMAX, PYMIN, GXMAX, GXMIN, GYMAX, GYMIN
      REAL(4)    :: XA, YA, XB, YB, XS, YS

      CALL INQGDEFIN(PXMIN,PXMAX,PYMIN,PYMAX, &
     &                      GXMIN,GXMAX,GYMIN,GYMAX)
      DX=(PXMAX-PXMIN)/(GXMAX-GXMIN)
      DY=(PYMAX-PYMIN)/(GYMAX-GYMIN)

      NAXL=ABS(NAX)
      NAYL=ABS(NAY)
      IF(NAXL.GT.NXMAXC) NAXL=NAXL-NXMAXC
      IF(NAYL.GT.NYMAXC) NAYL=NAYL-NYMAXC

      NBXL=ABS(NBX)
      NBYL=ABS(NBY)
      IF(NBXL.GT.NXMAXC) NBXL=NBXL-NXMAXC
      IF(NBYL.GT.NYMAXC) NBYL=NBYL-NYMAXC

      IF(NAX.GT.0) THEN
         NA=NXAC*(NAYL-1)+NAXL
         XA=DX*(X(NA)-GXMIN)+PXMIN
         YA=DY*(Y(NA)-GYMIN)+PYMIN
      ELSE
         NAX1=NAXL+1
         IF(NAX1.GT.NXMAXC) NAX1=NAX1-NXMAXC
         NAY1=NAYL+1
         IF(NAY1.GT.NYMAXC) NAY1=NAY1-NYMAXC
         NA1=NXAC*(NAYL-1)+NAX1
         NA2=NXAC*(NAY1-1)+NAXL
         XA=DX*(0.5*(X(NA1)+X(NA2))-GXMIN)+PXMIN
         YA=DY*(0.5*(Y(NA1)+Y(NA2))-GYMIN)+PYMIN
      ENDIF
      IF(NBX.GT.0) THEN
         NB=NXAC*(NBYL-1)+NBXL
         XB=DX*(X(NB)-GXMIN)+PXMIN
         YB=DY*(Y(NB)-GYMIN)+PYMIN
      ELSE
         NBX1=NBXL+1
         IF(NBX1.GT.NXMAXC) NBX1=NBX1-NXMAXC
         NBY1=NBYL+1
         IF(NBY1.GT.NYMAXC) NBY1=NBY1-NYMAXC
         NB1=NXAC*(NBYL-1)+NBX1
         NB2=NXAC*(NBY1-1)+NBXL
         XB=DX*(0.5*(X(NB1)+X(NB2))-GXMIN)+PXMIN
         YB=DY*(0.5*(Y(NB1)+Y(NB2))-GYMIN)+PYMIN
      ENDIF
      IF(UB.NE.UA) THEN
         XS=(XB-XA)*(U0-UA)/(UB-UA)+XA
         YS=(YB-YA)*(U0-UA)/(UB-UA)+YA
         IF(IND.EQ.1) THEN
            CALL MOVEPT(XS,YS,IPAT)
         ELSEIF(IND.EQ.-1) THEN
            CALL MOVEPT(XS,YS,-IPAT)
         ELSEIF(IND.EQ.0) THEN
            CALL DRAWPT(XS,YS)
         ENDIF
      ENDIF
!      WRITE(6,*) NAX,NAY,NBX,NBY,XS,YS
      RETURN
      END SUBROUTINE CONTS5

!     ****** CONTOUR PLOT : R-THETA, VARIABLE STEP, PATTERN ******

      SUBROUTINE CONTP6(Z,X,Y,NXA,NXMAX,NYMAX, &
     &                  ZORG,ZSTEP,NSTEP,IPAT,KA)

      USE GSCTR5
      IMPLICIT NONE
      INTEGER(4), INTENT(INOUT) :: NXA, NXMAX, NYMAX, NSTEP, IPAT
      INTEGER(4), DIMENSION(2,*), INTENT(INOUT) :: KA
      REAL(4),                    INTENT(INOUT) :: ZORG, ZSTEP
      REAL(4),  DIMENSION(*),     INTENT(INOUT) :: X, Y
      REAL(4),  DIMENSION(NXA,*), INTENT(INOUT) :: Z
      REAL(4)  :: EPS = 1.E-32
      EXTERNAL CONTS6

      IF(ABS(ZSTEP).LT.EPS) RETURN

      NXAC=NXA
      NXMAXC=NXMAX
      NYMAXC=NYMAX

      CALL CONTP0(Z,X,Y,NXA,NXMAX,NYMAX, &
     &            ZORG,ZSTEP,NSTEP,0,IPAT,KA,CONTS6)

      RETURN
      END SUBROUTINE CONTP6

!     ****** CONTOUR PLOT SLAVE ROUTINE : R-THETA VARIABLE ******

      SUBROUTINE CONTS6(NAX,NAY,NBX,NBY,U0,UA,UB,X,Y,IPAT,IND)

      USE GSCTR5
      IMPLICIT NONE
!      IMPLICIT LOGICAL(L)
      INTEGER(4), INTENT(INOUT) :: NAX, NAY, NBX, NBY, IPAT, IND
      REAL(4),    INTENT(INOUT) :: U0, UA, UB
      REAL(4), DIMENSION(*), INTENT(INOUT) :: X(*),Y(*)
      COMMON /GSCTR4/ RMAX, RT, TT, XT, YT
      REAL(4) :: RMAX, RT, TT, XT, YT
      INTEGER(4) :: NAX1, NAX2, NAY1, NAY2, NBX1, NBX2, NBY1, NBY2
      REAL(4)    :: DX, DY, PXMIN,PXMAX,PYMIN,PYMAX,GXMIN,GXMAX,GYMIN,GYMAX
      REAL(4)    :: XA, XB, XS, YA, YB, YS


      CALL INQGDEFIN(PXMIN,PXMAX,PYMIN,PYMAX, &
     &               GXMIN,GXMAX,GYMIN,GYMAX)
      DX=(PXMAX-PXMIN)/(GXMAX-GXMIN)
      DY=(PYMAX-PYMIN)/(GYMAX-GYMIN)

      IF(NAX.GT.0) THEN
         XA=X(NAX+(NAY-1)*NXAC)
         YA=Y(NAX+(NAY-1)*NXAC)
      ELSE
         NAX1=ABS(NAX)
         NAX2=ABS(NAX)+1
         IF(NAX2.GT.NXMAXC) NAX2=NAX2-NXMAXC
         NAY1=NAY
         NAY2=NAY+1
         IF(NAY2.GT.NYMAXC) NAY2=NAY2-NYMAXC
         XA=0.5D0*(X(NAX1+(NAY2-1)*NXAC)+X(NAX2+(NAY2-1)*NXAC))
         YA=0.5D0*(Y(NAX1+(NAY2-1)*NXAC)+Y(NAX2+(NAY2-1)*NXAC))
      ENDIF
!
      IF(NBX.GT.0) THEN
         XB=X(NBX+(NBY-1)*NXAC)
         YB=Y(NBX+(NBY-1)*NXAC)
      ELSE
         NBX1=ABS(NBX)
         NBX2=ABS(NBX)+1
         IF(NBX2.GT.NXMAXC) NBX2=NBX2-NXMAXC
         NBY1=NBY
         NBY2=NBY+1
         IF(NBY2.GT.NYMAXC) NBY2=NBY2-NYMAXC
         XB=0.5D0*(X(NBX1+(NBY2-1)*NXAC)+X(NBX2+(NBY2-1)*NXAC))
         YB=0.5D0*(Y(NBX1+(NBY2-1)*NXAC)+Y(NBX2+(NBY2-1)*NXAC))
      ENDIF

      XS=(XB-XA)*(U0-UA)/(UB-UA)+XA
      YS=(YB-YA)*(U0-UA)/(UB-UA)+YA
      XS=DX*(XS-GXMIN)+PXMIN
      YS=DY*(YS-GYMIN)+PYMIN

      IF(IND.EQ.1) THEN
         CALL MOVEPT(XS,YS,IPAT)
      ELSEIF(IND.EQ.-1) THEN
         CALL MOVEPT(XS,YS,-IPAT)
      ELSEIF(IND.EQ.0) THEN
         CALL DRAWPT(XS,YS)
      ENDIF
      XT=XS
      YT=YS
      RETURN
      END SUBROUTINE CONTS6
